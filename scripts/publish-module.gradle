apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'EnvPicker Android SDK'
                    url = 'https://github.com/bayerischer-rundfunk/android-env-picker'
                    licenses {
                        license {
                            name = 'Apache 2.0'
                            url = 'https://github.com/bayerischer-rundfunk/android-env-picker/blob/master/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'leonbusse'
                            name = 'Leon Busse'
                            email = 'leon.busse@br.de'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/bayerischer-rundfunk/android-env-picker.git'
                        developerConnection = 'scm:git:ssh://github.com/bayerischer-rundfunk/android-env-picker.git'
                        url = 'https://github.com/bayerischer-rundfunk/android-env-picker/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    if (shouldSignPublication()) {
        sign publishing.publications
    }
}

def shouldSignPublication() {
    // don't sign local releases used during development
    return !gradle.startParameter.taskNames.any { it.contains("PublicationToMavenLocal") }
}
